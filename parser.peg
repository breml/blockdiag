{
// blockdiag parser

package blockdiag
}

diag = _ (diagtype:("blockdiag" / "diagram") {
	_parserDiag = NewDiag()
	return diagtype, nil
} ) _ "{" _ diagElements* _ "}" _ EOF {
	updateEdges()
	_parserDiag.Name = "test"
	return _parserDiag, nil
}

// diagElements = diagAttr / chain / node
diagElements = diagAttr / chains

diagAttr = attrName:ident _ "=" _ attrValue:attrValue _ ";" _ {
	_parserDiag.Attributes[attrName.(string)] = attrValue.(string)
	return nil, nil
}

attrValue = ([a-zA-Z0-9]+) {
	return string(c.text), nil
}

chains = chain+

chain = node:node _ nodes:(edge _ n:node _ {
	return n, nil
} )* ";" _ {
	nodeA := node.(*Node)
	
	for _, n := range toIfaceSlice(nodes) {
		nodeB := n.(*Node)
		edge := nodeA.Name + "|" + nodeB.Name
		
		if e, present := _parserDiag.Edges[edge]; !present {
			e = &Edge{Name: edge, Start: nodeA, End: nodeB}
			_parserDiag.Edges[edge] = e
		}
		nodeA = nodeB
	}
	return nil, nil
}

node = node:ident {
	var n *Node
	var present bool
	
	name := string(c.text)
	if n, present = _parserDiag.Nodes[name]; !present {
		n = &Node{Name: name}
		_parserDiag.Nodes[name] = n
	} 
	return n, nil
}

edge = edge:("->") 

ident = [a-zA-Z0-9_]+ {
	return string(c.text), nil
}

_ = ([ \t\n\r] / Comment)*

Comment = "#" [^\n\r]* [\r]?[\n]

EOF = !.
